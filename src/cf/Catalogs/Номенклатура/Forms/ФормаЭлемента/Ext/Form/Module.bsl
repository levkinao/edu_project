
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидНоменклатурыПриИзменении(Элемент)
	
	ВидНоменклатурыПриИзмененииНаСервере();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()

	УстановитьВидимостьРеквизитовПоТипуНоменклатуры();
	
КонецПроцедуры

&НаСервере
Процедура ВидНоменклатурыПриИзмененииНаСервере()
	
	Объект.ТипНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидНоменклатуры, "ТипНоменклатуры");
	
	ЭтоТовар = Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар;
	Если Не ЭтоТовар Тогда
		Объект.НормативныйПроцентСухогоВещества = 0;
		Объект.ТребуетсяПроверкаЛабораторией = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьРеквизитовПоТипуНоменклатуры();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьРеквизитовПоТипуНоменклатуры()
	
	ЭтоТовар = Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар;
	
	Элементы.НормативныйПроцентСухогоВещества.Видимость = ЭтоТовар;
	Элементы.ТребуетсяПроверкаЛабораторией.Видимость = ЭтоТовар;
	
КонецПроцедуры

#КонецОбласти