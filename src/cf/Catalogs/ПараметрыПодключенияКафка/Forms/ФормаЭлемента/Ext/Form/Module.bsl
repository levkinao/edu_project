
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	
	ОчиститьСообщения();
	ПроверитьПодключениеКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьПодключениеКлиент()
	
	РезультатПодключения = Инициализация_Просмотр();
	Если РезультатПодключения.Успешно Тогда
		ТекстСообщения = НСтр("ru = 'Подключение успешно выполнено!'");
	Иначе
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Ошибка: %1'"), РезультатПодключения.ОписаниеОшибки);
	КонецЕсли;
	ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

&НаСервере
Функция Инициализация_Просмотр()
	
	РезультатИтог = Новый Структура;
	РезультатИтог.Вставить("Успешно", Ложь);
	РезультатИтог.Вставить("ОписаниеОшибки", "");     

	ПараметрыКлиентаАдминистрирования = РаботаСКафкаСервер.ПараметрыОбъектаКафка();
	
	РаботаСКафкаСервер.СоздатьКлиентАдминистрирования(ПараметрыКлиентаАдминистрирования);
	Если Не ПараметрыКлиентаАдминистрирования.Успешно Тогда
		ЗаполнитьЗначенияСвойств(РезультатИтог, ПараметрыКлиентаАдминистрирования);
		Возврат РезультатИтог;
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из РаботаСКафкаСервер.ГлобальныеПараметрыПросмотра() Цикл
		РезультатУстановкиПараметра = РаботаСКафкаСервер.КлиентАдминистрирования_УстановитьПараметрГлобальный(ПараметрыКлиентаАдминистрирования,
			КлючИЗначение.Ключ, КлючИЗначение.Значение);
		Если Не РезультатУстановкиПараметра.Успешно Тогда
			ЗаполнитьЗначенияСвойств(РезультатИтог, РезультатУстановкиПараметра);
			Возврат РезультатИтог;
		КонецЕсли;
	КонецЦикла;
	
	РезультатИнициализации = РаботаСКафкаСервер.КлиентАдминистрирования_Инициализировать(ПараметрыКлиентаАдминистрирования,
		Объект.Адрес);
	Если Не РезультатИнициализации.Успешно Тогда   
		ЗаполнитьЗначенияСвойств(РезультатИтог, РезультатИнициализации);
		Возврат РезультатИтог;
	КонецЕсли;
	
	РезультатИтог.Успешно = Истина;
	Возврат РезультатИтог;
КонецФункции

#КонецОбласти