
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	СформироватьДвиженияТоварыНаСкладах();
	СформироватьДвиженияРасходыСырьяНаПроизводствоКондитерскихИзделий();
	СформироватьДвиженияЗначенияПроцентаСухихВеществ();
	
	Движения.Записать();
	
	ВыполнитьКонтрольОстатковТоварыНаСкладах(Отказ);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияТоварыНаСкладах()
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ПроизводствоБезЗаказа.Дата КАК Период,
	|	ПроизводствоБезЗаказа.ВыходноеИзделие КАК Номенклатура,
	|	ПроизводствоБезЗаказа.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьСерии, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ПроизводствоБезЗаказа.Серия
	|	КОНЕЦ КАК Серия,
	|	ПроизводствоБезЗаказа.КоличествоФакт КАК Количество
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа КАК ПроизводствоБезЗаказа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ПроизводствоБезЗаказа.ВыходноеИзделие = СправочникНоменклатура.Ссылка
	|			И (ПроизводствоБезЗаказа.Ссылка = &Ссылка)
	|			И (СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (СправочникНоменклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ПроизводствоБезЗаказа.Дата,
	|	ПроизводствоБезЗаказаМатериалыИРаботы.Номенклатура,
	|	ПроизводствоБезЗаказа.Склад,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьСерии, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ПроизводствоБезЗаказаМатериалыИРаботы.Серия
	|	КОНЕЦ,
	|	СУММА(ПроизводствоБезЗаказаМатериалыИРаботы.КоличествоФакт)
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ПроизводствоБезЗаказаМатериалыИРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК ПроизводствоБезЗаказа
	|		ПО ПроизводствоБезЗаказаМатериалыИРаботы.Ссылка = ПроизводствоБезЗаказа.Ссылка
	|			И (ПроизводствоБезЗаказа.Ссылка = &Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ПроизводствоБезЗаказаМатериалыИРаботы.Номенклатура = СправочникНоменклатура.Ссылка
	|			И (СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (СправочникНоменклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроизводствоБезЗаказа.Дата,
	|	ПроизводствоБезЗаказаМатериалыИРаботы.Номенклатура,
	|	ПроизводствоБезЗаказа.Склад,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьСерии, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ПроизводствоБезЗаказаМатериалыИРаботы.Серия
	|	КОНЕЦ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Движение = Движения.ТоварыНаСкладах.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура СформироватьДвиженияРасходыСырьяНаПроизводствоКондитерскихИзделий()
	
	Движения.РасходыСырьяНаПроизводствоКондитерскихИзделий.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПроизводствоБезЗаказа.Дата КАК Период,
	|	ПроизводствоБезЗаказаМатериалыИРаботы.Номенклатура КАК Номенклатура,
	|	СУММА(ПроизводствоБезЗаказаМатериалыИРаботы.КоличествоПлан) КАК КоличествоПлан,
	|	СУММА(ПроизводствоБезЗаказаМатериалыИРаботы.КоличествоФакт) КАК КоличествоФакт,
	|	СУММА(ПроизводствоБезЗаказаМатериалыИРаботы.КоличествоПлан * ПроизводствоБезЗаказаМатериалыИРаботы.ПроцентСухогоВеществаПлан / 100) КАК КоличествоВСухомВеществеПлан,
	|	СУММА(ПроизводствоБезЗаказаМатериалыИРаботы.КоличествоФакт * ПроизводствоБезЗаказаМатериалыИРаботы.ПроцентСухогоВеществаФакт / 100) КАК КоличествоВСухомВеществеФакт
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ПроизводствоБезЗаказаМатериалыИРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК ПроизводствоБезЗаказа
	|		ПО ПроизводствоБезЗаказаМатериалыИРаботы.Ссылка = ПроизводствоБезЗаказа.Ссылка
	|			И (ПроизводствоБезЗаказа.Ссылка = &Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ПроизводствоБезЗаказаМатериалыИРаботы.Номенклатура = СправочникНоменклатура.Ссылка
	|			И (СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроизводствоБезЗаказа.Дата,
	|	ПроизводствоБезЗаказаМатериалыИРаботы.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Движение = Движения.РасходыСырьяНаПроизводствоКондитерскихИзделий.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура СформироватьДвиженияЗначенияПроцентаСухихВеществ()
	
	Движения.ЗначенияПроцентаСухихВеществ.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПроизводствоБезЗаказа.Дата КАК Период,
	|	ПроизводствоБезЗаказа.ВыходноеИзделие КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьСерии, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ПроизводствоБезЗаказа.Серия
	|	КОНЕЦ КАК Серия,
	|	ПроизводствоБезЗаказа.ПроцентСухогоВеществаФакт КАК ПроцентСухогоВещества
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа КАК ПроизводствоБезЗаказа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ПроизводствоБезЗаказа.ВыходноеИзделие = СправочникНоменклатура.Ссылка
	|			И (СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))
	|			И (СправочникНоменклатура.ТребуетсяПроверкаЛабораторией)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (СправочникНоменклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Движение = Движения.ЗначенияПроцентаСухихВеществ.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура ВыполнитьКонтрольОстатковТоварыНаСкладах(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроизводствоБезЗаказаМатериалыИРаботы.Номенклатура КАК Номенклатура,
	|	ПроизводствоБезЗаказа.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьСерии, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ПроизводствоБезЗаказаМатериалыИРаботы.Серия
	|	КОНЕЦ КАК Серия
	|ПОМЕСТИТЬ втИзмерения
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ПроизводствоБезЗаказаМатериалыИРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК ПроизводствоБезЗаказа
	|		ПО ПроизводствоБезЗаказаМатериалыИРаботы.Ссылка = ПроизводствоБезЗаказа.Ссылка
	|			И (ПроизводствоБезЗаказа.Ссылка = &Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ПроизводствоБезЗаказаМатериалыИРаботы.Номенклатура = СправочникНоменклатура.Ссылка
	|			И (СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (СправочникНоменклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстатки.Склад КАК Склад,
	|	ТоварыНаСкладахОстатки.Серия КАК Серия,
	|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			&ПериодГраница,
	|			(Номенклатура, Склад, Серия) В
	|				(ВЫБРАТЬ
	|					втИзмерения.Номенклатура,
	|					втИзмерения.Склад,
	|					втИзмерения.Серия
	|				ИЗ
	|					втИзмерения КАК втИзмерения)) КАК ТоварыНаСкладахОстатки
	|ГДЕ
	|	ТоварыНаСкладахОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПериодГраница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		ШаблонСообщения = НСтр("ru = 'Номенклатура %1 %2
			|Превышен оперативный складской остаток на складе ""%3"" на %4'");

		Пока Выборка.Следующий() Цикл
			ТекстСообщения = СтрШаблон(ШаблонСообщения,
										Выборка.Номенклатура,
										Выборка.Серия,
										Выборка.Склад,
										-Выборка.Количество);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, , , Отказ);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
