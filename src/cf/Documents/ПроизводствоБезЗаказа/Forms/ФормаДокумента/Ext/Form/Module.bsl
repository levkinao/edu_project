
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВыходноеИзделиеПриИзменении(Элемент)
	
	Объект.ПроцентСухогоВеществаПлан = 0;
	Объект.ПроцентСухогоВеществаФакт = 0;
	
	УстановитьВидимостьРеквизитовПоТипуНоменклатурыВыходногоИзделия();

КонецПроцедуры

&НаКлиенте
Процедура РесурснаяСпецификацияПриИзменении(Элемент)
	
	 РесурснаяСпецификацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПланПриИзменении(Элемент)
	
	Объект.КоличествоФакт = Объект.КоличествоПлан;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМатериалыИРаботы

&НаКлиенте
Процедура МатериалыИРаботыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МатериалыИРаботы.ТекущиеДанные;

	ЗаполнитьСлужебныеРеквизитыНоменклатурыВТаблицеМатериалыИРаботы(Новый Структура("НомерСтроки", ТекущиеДанные.НомерСтроки));
	
	Если ТекущиеДанные.ТипНоменклатуры <> ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар") Тогда
		Объект.ПроцентСухогоВеществаПлан = 0;
		Объект.ПроцентСухогоВеществаФакт = 0;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура МатериалыИРаботыКоличествоПланПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МатериалыИРаботы.ТекущиеДанные;
	
	ТекущиеДанные.КоличествоФакт = ТекущиеДанные.КоличествоПлан;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоСпецификации(Команда)
	
	ОчиститьСообщения();
	
	ЗаполнитьПоСпецификацииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()

	ЗаполнитьСлужебныеРеквизитыНоменклатурыВТаблицеМатериалыИРаботы();
	
	УстановитьВидимостьРеквизитовПоТипуНоменклатурыВыходногоИзделия();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьРеквизитовПоТипуНоменклатурыВыходногоИзделия()
	
	ЭтоТовар = НоменклатураЯвляетсяТоваром(Объект.ВыходноеИзделие);
	
	Элементы.ПроцентСухогоВеществаПлан.Видимость = ЭтоТовар;
	Элементы.ПроцентСухогоВеществаФакт.Видимость = ЭтоТовар;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НоменклатураЯвляетсяТоваром(Номенклатура)
	
	ТипНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ТипНоменклатуры");
	Возврат ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар;

КонецФункции

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыНоменклатурыВТаблицеМатериалыИРаботы(Строки = Неопределено)

	Таблица = Объект.МатериалыИРаботы;
	
	Если Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.ТипНоменклатуры КАК ТипНоменклатуры,
	|	Таблица.ПроцентСухогоВеществаПлан КАК ПроцентСухогоВеществаПлан
	|ПОМЕСТИТЬ Таблица
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(СправочникНоменклатура.ТипНоменклатуры, ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПустаяСсылка)) КАК ТипНоменклатуры,
	|	ЕСТЬNULL(СправочникНоменклатура.НормативныйПроцентСухогоВещества, 0) КАК ПроцентСухогоВеществаПлан
	|ИЗ
	|	Таблица КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Таблица.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	(Таблица.ТипНоменклатуры <> ЕСТЬNULL(СправочникНоменклатура.ТипНоменклатуры, ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПустаяСсылка))
	|			ИЛИ Таблица.ПроцентСухогоВеществаПлан <> ЕСТЬNULL(СправочникНоменклатура.НормативныйПроцентСухогоВещества, 0))";
	
	Запрос.УстановитьПараметр("Таблица", Таблица.Выгрузить(Строки, "НомерСтроки, Номенклатура, ТипНоменклатуры, ПроцентСухогоВеществаПлан"));

	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Строка = Таблица[Выборка.НомерСтроки - 1];
		ЗаполнитьЗначенияСвойств(Строка, Выборка,, "НомерСтроки");
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РесурснаяСпецификацияПриИзмененииНаСервере()
	
	Объект.ПроцентСухогоВеществаПлан = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.РесурснаяСпецификация,
		"НормативныйПроцентСухогоВещества");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоСпецификацииНаСервере()
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.РесурснаяСпецификация) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Ресурсная спецификация'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,, "Объект.РесурснаяСпецификация", Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.КоличествоПлан) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Количество (план)'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,, "Объект.КоличествоПлан", Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Объект.МатериалыИРаботы.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РесурсныеСпецификацииМатериалыИРаботы.Номенклатура КАК Номенклатура,
	|	РесурсныеСпецификацииМатериалыИРаботы.Количество * ВЫБОР
	|		КОГДА РесурсныеСпецификации.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ &КоличествоВыходногоИзделия / РесурсныеСпецификации.Количество
	|	КОНЕЦ КАК КоличествоПлан
	|ИЗ
	|	Справочник.РесурсныеСпецификации.МатериалыИРаботы КАК РесурсныеСпецификацииМатериалыИРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
	|		ПО РесурсныеСпецификацииМатериалыИРаботы.Ссылка = РесурсныеСпецификации.Ссылка
	|			И (РесурсныеСпецификации.Ссылка = &РесурснаяСпецификация)";
	
	Запрос.УстановитьПараметр("РесурснаяСпецификация", Объект.РесурснаяСпецификация);
	Запрос.УстановитьПараметр("КоличествоВыходногоИзделия", Объект.КоличествоПлан);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СтрокаТаблицы = Объект.МатериалыИРаботы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
			СтрокаТаблицы.КоличествоФакт = СтрокаТаблицы.КоличествоПлан;
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаполнитьСлужебныеРеквизитыНоменклатурыВТаблицеМатериалыИРаботы();
	
КонецПроцедуры

#КонецОбласти