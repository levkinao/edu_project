
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	СформироватьДвиженияЗначенияПроцентаСухихВеществ();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияЗначенияПроцентаСухихВеществ()
	
	Движения.ЗначенияПроцентаСухихВеществ.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЛабораторноеИсследование.Дата КАК Период,
	|	ЛабораторноеИсследованиеТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьСерии, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ЛабораторноеИсследованиеТовары.Серия
	|	КОНЕЦ КАК Серия,
	|	МАКСИМУМ(ЛабораторноеИсследованиеТовары.ПроцентСухогоВещества) КАК ПроцентСухогоВещества
	|ИЗ
	|	Документ.ЛабораторноеИсследование.Товары КАК ЛабораторноеИсследованиеТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЛабораторноеИсследование КАК ЛабораторноеИсследование
	|		ПО ЛабораторноеИсследованиеТовары.Ссылка = ЛабораторноеИсследование.Ссылка
	|			И (ЛабораторноеИсследование.Ссылка = &Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ЛабораторноеИсследованиеТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|			И (СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))
	|			И (СправочникНоменклатура.ТребуетсяПроверкаЛабораторией)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (СправочникНоменклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЛабораторноеИсследование.Дата,
	|	ЛабораторноеИсследованиеТовары.Номенклатура,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьСерии, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ЛабораторноеИсследованиеТовары.Серия
	|	КОНЕЦ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Движение = Движения.ЗначенияПроцентаСухихВеществ.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
