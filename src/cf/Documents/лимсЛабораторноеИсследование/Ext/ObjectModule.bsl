
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.лимсПриобретениеТоваровУслуг") Тогда
		ЗаполнитьДокументНаОснованииПриобретенияТоваровУслуг(ДанныеЗаполнения);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлудебныеПроцедурыИФункции

Процедура ЗаполнитьДокументНаОснованииПриобретенияТоваровУслуг(Знач ДокументОснование)

	ЭтотОбъект.ДокументОснование = ДокументОснование;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	лимсПриобретениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	лимсПриобретениеТоваровУслугТовары.Серия КАК Серия
	|ИЗ
	|	Документ.лимсПриобретениеТоваровУслуг.Товары КАК лимсПриобретениеТоваровУслугТовары
	|ГДЕ
	|	лимсПриобретениеТоваровУслугТовары.Ссылка = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Серия
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Товары.Очистить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();	
		
		Пока Выборка.Следующий() Цикл
			СтрокаТовары = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТовары, Выборка);
		КонецЦикла; 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
